plugins {
    id "com.google.protobuf" version "0.9.3"
    id 'org.jetbrains.kotlin.android'
}

apply from: "$rootDir/gradle/android.gradle"

android {
    defaultConfig {
        vectorDrawables {
            useSupportLibrary true
        }
    }
    namespace "org.radarbase.passive.google.healthconnect"
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
            "-P",
            "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=1.7.21"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
}

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

description = 'Google Health Connect integration'

repositories {
    mavenLocal()
}

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

dependencies {
    api project(':radar-commons-android')

    implementation("org.radarbase:radar-schemas-commons:0.8.5-connect-SNAPSHOT") {
        exclude group: 'org.apache.avro', module: 'avro'
    }
    // Add a dependency of Health Connect SDK
    implementation("androidx.health.connect:connect-client:1.0.0-alpha11")
    implementation("androidx.datastore:datastore:1.0.0")
    implementation  "com.google.protobuf:protobuf-javalite:3.23.3"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2023.06.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.23.2'
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

apply from: "$rootDir/gradle/publishing.gradle"
