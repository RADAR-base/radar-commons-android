/*
 * Copyright 2017 The Hyve
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlin_version = '1.7.21'
    ext.dokka_version = '1.8.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.bjoernq:unmockplugin:0.7.9'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version")
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
    id("com.github.ben-manes.versions") version "0.46.0"
    id 'org.jetbrains.kotlin.android' version '1.7.21' apply false
}

allprojects {
    ext.githubRepoName = 'RADAR-base/radar-commons-android'

    ext.githubUrl = 'https://github.com/' + githubRepoName + '.git'
    ext.issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    ext.website = 'http://radar-base.org'

    version = "1.2.6-cordova-SNAPSHOT"
    group = 'org.radarbase'

    ext.versionCode = 50
}

subprojects {
    if (it.name == 'radar-android-avro') {
        return
    }
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'idea'

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

    repositories {
        google()
        mavenCentral()
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }

    idea {
        module {
            downloadSources = true
        }
    }

    tasks.matching { it instanceof Test }.all {
        testLogging {
            showExceptions = true
            showStandardStreams = true
            showCauses = true
            showStackTraces = true
            exceptionFormat 'full'
        }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '11'
            apiVersion = '1.7'
            languageVersion = '1.7'
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.hasProperty("ossrh.user") ? project.property("ossrh.user") : System.getenv("OSSRH_USER")
            password = project.hasProperty("ossrh.password") ? project.property("ossrh.password") : System.getenv("OSSRH_PASSWORD")
        }
    }
}

wrapper {
    gradleVersion '8.1.1'
}
